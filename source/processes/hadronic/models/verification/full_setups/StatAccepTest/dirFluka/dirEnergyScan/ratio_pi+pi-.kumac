************************************************************************
* Last update: 15-Aug-2008
*
* This Kumac uses the files:   input1.txt   and   input2.txt
* in the current directory.
* These files should be text files with the visible energy (in MeV)
* as a function of the beam energy (in GeV) in a simplified calorimeter,
* for Fluka, for pion- and pion+, respectively.
* For instance:
*   $  ln -s result-pi-cms-fluka-evis.txt  result1.txt
*   $  ln -s result-pi+-cms-fluka-evis.txt result2.txt
* where result{1,2}.txt looks like:
*               fluka   
*   1GeV Evis    8.1 
*   2GeV Evis   16.2
*   ...          ...
*   30GeV Evis  247.2
* 
* The Kumac shows a plot of the ratio:
*
*           Visible Energy for  pi+  incident particle  (i.e. result2.txt)
*         --------------------------------------------
*           Visible Energy for  pi-  incident particle  (i.e. result1.txt)
*
* as a function of the Beam Energy and then save it in the file  ratio.eps
*
************************************************************************
*
vec/create vx(30)
vec/input vx 1. 2. 3. 4. 5. 6. 7. 8. 9. 10. 11. 12. 13. 14. 15. 16. 17. 18. 19. 20. 21. 22. 23. 24. 25. 26. 27. 28. 29. 30.
vec/print vx
wait
*
shell sed -e s:%:\ :g -e s:.*GeV\ res:\ :g -e s:.*GeV\ Evis:\ :g -e /fluka/d result1.txt > input1.txt
*
vec/read fluka1 input1.txt
*
message  ' '
message  '============='
message  '===  pi-  ==='
message  '============='
message  ' '
vec/print fluka1
wait
*
shell sed -e s:%:\ :g -e s:.*GeV\ res:\ :g -e s:.*GeV\ Evis:\ :g -e /fluka/d result2.txt > input2.txt
*
vec/read fluka2 input2.txt
*
message  ' '
message  '============='
message  '===  pi+  ==='
message  '============='
message  ' '
vec/print fluka2
wait
*
*
message  ' '
message  '======================='
message  '===  ratio pi+/pi-  ==='
message  '======================='
message  ' '
SIGMA rfluka = fluka2/fluka1
vec/print rfluka
wait
*
*
title 'Ratio pi+/pi- Visible Energy  CMS HCAL  Fluka'
***title 'Ratio pi+/pi- Visible Energy  ATLAS HCAL  Fluka'
***title 'Ratio pi+/pi- Visible Energy  PbWO4  Fluka'
***title 'Ratio pi+/pi- Visible Energy  TILECAL  Fluka'
*
for/file 20 ratio.eps
meta 20 -113
*
opt grid
opt tic
LABEL 1 30 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30
SET NDVX 30.15
NULL 1.0 31.0 0.9 1.1
*
atitle 'Ebeam (GeV)' 'Ratio pi+/pi- Visible Energy'
*
set pmci 1
GRAPHICS/HPLOT/SYMBOLS vx rfluka 30 20 0.20 
*
wait
*
close 20
*

