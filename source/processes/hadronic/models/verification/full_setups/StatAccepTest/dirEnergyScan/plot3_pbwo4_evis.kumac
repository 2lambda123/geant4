************************************************************************
* Last update: 21-Jul-2008
*
* This Kumac uses the file:   result.txt
* in the current directory.
* This file should be a text file with the visible energy (in MeV)
* as a function of the beam energy (in GeV) in a simplified calorimeter,
* for 3 Physics Lists.
* For instance:
*   $  ln -s dir9.2.b01/result-pi-cms-9.2.b01-evis.txt result.txt
* where result.txt looks like:
*                FTFP    FTF_BIC  FTFP_BERT
*   1GeV Evis    7.4     11.9     13.3
*   2GeV Evis    14.8    23.9     23.4 
*   ...
*   30GeV Evis   254.3   320.6    321.1
* 
* The Kumac shows a plot of the ratio  Visible Energy / Beam Energy
* as a function of the Beam Energy and then save it in the file  plot.ps
*
************************************************************************
*
vec/create vx(30)
vec/input vx 1. 2. 3. 4. 5. 6. 7. 8. 9. 10. 11. 12. 13. 14. 15. 16. 17. 18. 19. 20. 21. 22. 23. 24. 25. 26. 27. 28. 29. 30.
vec/print vx
wait
*
shell sed -e s:%:\ :g -e s:.*GeV\ res:\ :g -e s:.*GeV\ Evis:\ :g -e /P/d result.txt > input.txt
* 
vec/read ftfp,fbic,fbert input.txt
*
vec/print ftfp
wait
vec/print fbic
wait
vec/print fbert
wait
*
SIGMA rftfp = ftfp/(vx*1000.0)
vec/print rftfp
wait
SIGMA rfbic = fbic/(vx*1000.0)
vec/print rfbic
wait
SIGMA rfbert = fbert/(vx*1000.0)
vec/print rfbert
wait
*
***title 'Evis / Ebeam  vs.  Ebeam  PbWO4 pi-  G4 9.2.b01'
title 'Evis / Ebeam  vs.  Ebeam  PbWO4 p  G4 9.2.b01'
*
for/file 20 plot.ps
meta 20 -111
*
opt grid
opt tic
LABEL 1 30 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30
SET NDVX 30.15
NULL 1.0 31.0 0.0 0.25
*
atitle 'Ebeam (GeV)' 'Evis / Ebeam'
*
set pmci 4
GRAPHICS/HPLOT/SYMBOLS vx rftfp 30 23 0.20 
key 22.0 0.23 23 'FTFP'
*
set pmci 7
GRAPHICS/HPLOT/SYMBOLS vx rfbic 30 26 0.20 
key 22.0 0.215 26 'FTF_BIC'
*
set pmci 1
GRAPHICS/HPLOT/SYMBOLS vx rfbert 30 27 0.20 
key 22.0 0.20 27 'FTFP_BERT'
*
wait
*
* --- Individual plots ---
*
NULL 1.0 31.0 0.0 0.25
set pmci 4
GRAPHICS/HPLOT/SYMBOLS vx rftfp 30 23 0.20 
key 22.0 0.23 23 'FTFP'
atitle 'Ebeam (GeV)' 'Evis / Ebeam'
wait
*
NULL 1.0 31.0 0.0 0.25
set pmci 7
GRAPHICS/HPLOT/SYMBOLS vx rfbic 30 26 0.20 
key 22.0 0.215 26 'FTF_BIC'
atitle 'Ebeam (GeV)' 'Evis / Ebeam'
wait
*
NULL 1.0 31.0 0.0 0.25
set pmci 1
GRAPHICS/HPLOT/SYMBOLS vx rfbert 30 27 0.20 
key 22.0 0.20 27 'FTFP_BERT'
atitle 'Ebeam (GeV)' 'Evis / Ebeam'
wait
*
close 20
*

