*-------------------------------------------------------------
* 29-Apr-2005    Compare sampling fraction vs. beam energy.  
*                Input:  beam_energies.txt : beam energy values;
*                        sampling_fractions.txt-a  
*                        sampling_fractions.txt-b  
*               The kumac should be able to work even if one
*               of the two input files is missing.
*-------------------------------------------------------------
*
MESSAGE ' '
MESSAGE '  --- START KUMAC  sampling_fraction.kumac  --- '
*
xmin = 999.9
xmax = 0.0
IF ($FEXIST(beam_energies.txt)) THEN
  VEC/READ vx beam_energies.txt
  xmin = 0.0
  xmax = $RSIGMA(1.0+int(VMAX(vx)*1.1))
***  MESSAGE ' xmax=', [xmax]
ELSE
  MESSAGE ' File beam_energies.txt NOT found! '
  exit
ENDIF
*
y1min = 999.9
y1max = 0.0   
IF ($FEXIST(sampling_fractions.txt-a)) THEN
  VEC/READ vy1 sampling_fractions.txt-a
***  MESSAGE ' vy1 : '
***  vec/print vy1
***  y1min = $RSIGMA(VMIN(vy1)*0.9)
  y1min = 0.0
  y1max = $RSIGMA(VMAX(vy1)*1.1)
***  MESSAGE ' y1min=', [y1min]
***  MESSAGE ' y1max=', [y1max]
ENDIF
*
y2min = 999.9
y2max = 0.0 
IF ($FEXIST(sampling_fractions.txt-b)) THEN
  VEC/READ vy2 sampling_fractions.txt-b
***  message ' vy2 : '
***  vec/print vy2
***  y2min = $RSIGMA(VMIN(vy2)*0.9)
  y2min = 0.0
  y2max = $RSIGMA(VMAX(vy2)*1.1)
***  MESSAGE ' y2min=', [y2min]
***  MESSAGE ' y2max=', [y2max]
ENDIF
*
ymin = [y1min]
IF ( [y2min].LT.[ymin] ) THEN
   ymin = [y2min]
ENDIF
***MESSAGE ' ymin=' [ymin]
*
ymax = [y1max]
IF ( [y2max].GT.[ymax] ) THEN
   ymax = [y2max]
ENDIF
***MESSAGE ' ymax=' [ymax]
*
FOR/FILE 20 sampling_fraction.ps
META 20 -111
*
TITLE 'Sampling fraction vs. beam energy'
OPT UTIT
ZONE 1 1
SET KSIZ 0.3
*
NULL [xmin] [xmax] [ymin] [ymax]
*
IF ($FEXIST(sampling_fractions.txt-a)) THEN
  SET PMCI 2
  GRAPHICS/HPLOT/SYMBOLS vx vy1 $VLEN(vy1,1) 20 0.20 
ENDIF
IF ($FEXIST(sampling_fractions.txt-b)) THEN
  SET PMCI 4
  GRAPHICS/HPLOT/SYMBOLS vx vy2 $VLEN(vy2,1) 21 0.20
ENDIF
*
ATITLE 'Beam energy' 'Sampling fraction'
*
CLOSE 20
*
MESSAGE '  --- END KUMAC  sampling_fraction.kumac  --- '
MESSAGE ' '
