#!/bin/csh -f
#$Id: configure,v 1.2 2006-04-25 05:42:14 kmura Exp $
# ======================================================================
#  A configure script for Geant4Py
# ======================================================================
setenv LANG C

# default values
set prefix   = $cwd

set g4_incdir = /zzz
if ( $?G4INCLUDE ) then
  set g4_incdir = $G4INCLUDE
endif

set g4_libdir = /zzz
if ( $?G4LIB ) then
  set g4_libdir = $G4LIB/$G4SYSTEM
endif

set g4_version = "auto"
if ( $?G4VERSION_NUMBER ) then
  set g4_version = $G4VERSION_NUMBER
endif

set clhep_incdir = /zzz
if ( $?CLHEP_INCLUDE_DIR ) then
  set clhep_incdir = $CLHEP_INCLUDE_DIR
endif

set clhep_libdir = /zzz
if ( $?CLHEP_LIB_DIR ) then
  set clhep_libdir = $CLHEP_LIB_DIR
endif

set clhep_lib = CLHEP
if ( $?CLHEP_LIB ) then
  set clhep_lib = $CLHEP_LIB
endif

set python_incdir = /usr/include/python
set boost_incdir = /usr/include
set boost_python_lib = boost_python

set enable_openglx = 1

set enable_openglxm = 0
if ( $?G4VIS_USE_OPENGLXM ) then
  set enable_openglxm = 1
endif

set enable_raytracerx = 0
if ( $?G4VIS_USE_RAYTRACERX ) then
  set enable_raytracerx = 1
endif

# options
set options = `cat` <<EOF
-l help 
-l prefix: 
-l libdir: 
-l incdir:
-l with-package-dir:
-l with-g4-incdir:
-l with-g4-libdir:
-l with-g4-version:
-l with-clhep-incdir:
-l with-clhep-libdir:
-l with-clhep-lib:
-l with-python-incdir:
-l with-python-libdir:
-l with-boost-incdir:
-l with-boost-libdir:
-l with-boost-python-lib:
-l enable-openglx
-l disable-openglx
-l enable-openglxm
-l disable-openglxm
-l enable-raytracerx
-l disable-raytracerx
h
EOF

# option analysis
set opt = `getopt -u $options $argv`
if ( $status != 0 ) then
  exit(-1)
endif

while ( "$opt[1]" != "--" )
  switch ($opt[1])
    case --help:
      goto HELP
      breaksw
    case -h:
      goto HELP
      breaksw
    case --prefix:
      shift opt
      set prefix = "$opt[1]"
      breaksw
    case --libdir:
      shift opt
      set libdir = "$opt[1]"
      breaksw
    case --with-package-dir:
      shift opt
      set g4_incdir    = "$opt[1]/include/Geant4"
      set g4_libdir    = "$opt[1]/lib"
      set clhep_incdir = "$opt[1]/include"
      set clhep_libdir = "$opt[1]/lib"
      breaksw
    case --with-g4-incdir:
      shift opt
      set g4_incdir = "$opt[1]"
      breaksw
    case --with-g4-libdir:
      shift opt
      set g4_libdir = "$opt[1]"
      breaksw
    case --with-g4-version:
      shift opt
      set g4_version = "$opt[1]"
      breaksw
    case --with-clhep-incdir:
      shift opt
      set clhep_incdir = "$opt[1]"
      breaksw
    case --with-clhep-libdir:
      shift opt
      set clhep_libdir = "$opt[1]"
      breaksw
    case --with-clhep-lib:
      shift opt
      set clhep_lib = "$opt[1]"
      breaksw
    case --with-python-incdir:
      shift opt
      set python_incdir = "$opt[1]"
      breaksw
    case --with-python-libdir:
      shift opt
      set python_libdir = "$opt[1]"
      breaksw
    case --with-boost-incdir:
      shift opt
      set boost_incdir = "$opt[1]"
      breaksw
    case --with-boost-libdir:
      shift opt
      set boost_libdir = "$opt[1]"
      breaksw
    case --with-boost-python-lib:
      shift opt
      set boost_python_lib = "$opt[1]"
      breaksw
    case --enable-openglx:
      set enable_openglx = 1
      breaksw
    case --disable-openglx:
      set enable_openglx = 0
      breaksw
    case --enable-openglxm:
      set enable_openglxm = 1
      breaksw
    case --disable-openglxm:
      set enable_openglxm = 0
      breaksw
    case --enable-raytracerx:
      set enable_raytracerx = 1
      breaksw
    case --disable-raytracerx:
      set enable_raytracerx = 0
      breaksw
  endsw
  shift opt
end
shift opt

# check variables
if ( $#opt !=  1 ) then
  echo "Error: System type is not specified or more than one system type. "
  echo ""
  echo "Usage:  ./configure SYSTEM [OPTION]... [VAR=VALUE]..."
  echo ""
  exit (-1)
endif

set system = $opt[1]
echo -n "Checking for system type ... "
if ( ! -e config/sys/$system.gmk ) then
  echo "no"
  echo -n "### Invalid system type. "
  echo "Please taka a look in config/sys/."
  exit (-1)
endif
echo "$system"

set arch = 32
if ( $system == linuxx8664gcc ) then
  set arch = 64
endif

echo -n "Checking for prefix ... "
echo $prefix

echo -n "Checking for lib dir ... "
if ( ! $?libdir ) then
  set libdir   = $prefix/lib
endif
echo $libdir

echo -n "Checking for include dir ... "
if ( ! $?incdir ) then
  set incdir   = $prefix/include
endif
echo $libdir

echo -n "Checking for G4 include dir ... "
if ( ! -d $g4_incdir ) then
  echo "no"
  echo -n "### $g4_incdir (g4-incdir) does not exist. "
  echo "Try --with-g4-incdir option."
  exit(-1)
endif
echo "$g4_incdir"

echo -n "Checking for G4 lib dir ... "
if ( ! -d $g4_libdir ) then
  echo "no"
  echo -n "### $g4_libdir (g4-libdir) does not exist. "
  echo "Try --with-g4-libdir option."
  exit(-1)
endif
echo "$g4_libdir"

echo -n "Checking for G4 version ... "
if ( !( $g4_version == "auto" || $g4_version == "700" || \
     $g4_version == "701" || $g4_version == "710" || \
     $g4_version == "711" ) ) then
  echo "no"
  echo "### $g4_version (g4-version) is invalid (700/701/710/711)."
  exit(-1)
endif
echo "$g4_version"

if ( $g4_version == "auto" ) then
  if ( ! -f $g4_incdir/G4Version.hh ) then
    echo "### G4Version.hh does not exist. Try --with-g4-version option."
    exit(-1)
  endif
endif

# ---
echo -n "Checking for CLHEP include dir ... "
if ( ! -d $clhep_incdir ) then
  echo "no"
  echo -n "### $clhep_incdir (clhep-incdir) does not exist. "
  echo "Try --with-clhep-incdir option."
  exit(-1)
endif
echo "$clhep_incdir"

echo -n "Checking for CLHEP lib dir ... "
if ( ! -d $clhep_libdir ) then
  echo "no"
  echo -n "### $clhep_libdir (clhep-libdir) does not exist. "
  echo "Try --with-clhep-libdir option."
  exit(-1)
endif
echo "$clhep_libdir"

echo -n "Checking for CLHEP lib name ... "
if ( ! -f $clhep_libdir/lib${clhep_lib}.so ) then
  echo "no"
  echo -n "### lib${clhep_lib}.so (clhep-lib) does not exist. "
  echo "Try --with-clhep-lib option."
  exit(-1)
endif
echo "lib$clhep_lib.so"

# ---
echo -n "Checking for Python include dir (pyconfig.h) ... "
if ( ! -f $python_incdir/pyconfig.h ) then
  echo "no"
  echo -n "### $python_incdir (python-incdir) is invalid. "
  echo "Try --with-python-incdir option."
  exit(-1)
endif
echo "$python_incdir"

echo -n "Checking for Python lib dir ... "
if ( ! $?python_libdir ) then
  if ( $arch == 64) then
    set python_libdir = /usr/lib64
  else
    set python_libdir = /usr/lib
  endif
endif
if ( ! -d $python_libdir ) then
  echo "no"
  echo -n "### $python_libdir (python-libdir) does not exist. "
  echo "Try --with-python-libdir option."
  exit(-1)
endif
echo "$python_libdir"

# ---
echo -n "Checking for Boost include dir (boost/python.hpp) ... "
if ( ! -f $boost_incdir/boost/python.hpp ) then
  echo "no"
  echo -n "### $boost_incdir (boost-incdir) is invalid. "
  echo "Try --with-boost-incdir option."
  exit(-1)
endif
echo "$boost_incdir"

echo -n "Checking for Boost lib dir ... "
if ( ! $?boost_libdir ) then
  if ( $arch == 64) then
    set boost_libdir = /usr/lib64
  else
    set boost_libdir = /usr/lib
  endif
endif
if ( ! -d $boost_libdir ) then
  echo "no"
  echo -n "### $boost_libdir (boost-libdir) does not exist. "
  echo "Try --with-boost-libdir option."
  exit(-1)
endif
echo "$boost_libdir"

echo -n "Checking for Boost Python lib name ... "
if ( ! -f $boost_libdir/lib$boost_python_lib.so ) then
  echo "no"
  echo -n "### lib$boost_python_lib.so (boost-python-lib) does not exist. "
  echo "Try --with-boost-libdir option."
  exit(-1)
endif
echo "lib$boost_python_lib.so"

# ---
echo -n "Checking for OpenGL support ..."
if ( -f $g4_libdir/libG4OpenGL.so ) then
  if ( $enable_openglx == 0 ) then
    echo "disabled"
  else
    echo "yes"
  endif
else
  echo "no"
  set enable_openglx = 0
endif

# ---
echo -n "Writing config.gmk ... "

set make_config = config/config.gmk

cat > $make_config <<EOF
# config.gmk
#
# Automatically generated by configure script
#
# Makefile definitions included by the top Makefile

G4PY_INSTALL          := $cwd

Q_SYSTEM              := $system

Q_PREFIX              := $prefix
Q_LIBDIR              := $libdir
Q_INCDIR              := $incdir

Q_G4_INCDIR           := $g4_incdir
Q_G4_LIBDIR           := $g4_libdir
EOF

if ( $g4_version != auto ) then
cat >> $make_config <<EOF
Q_G4_VERSION          := $g4_version
EOF
endif

cat >> $make_config <<EOF

Q_CLHEP_INCDIR        := $clhep_incdir
Q_CLHEP_LIBDIR        := $clhep_libdir
Q_CLHEP_LIB           := $clhep_lib

Q_PYTHON_INCDIR       := $python_incdir
Q_PYTHON_LIBDIR       := $python_libdir
Q_BOOST_INCDIR        := $boost_incdir
Q_BOOST_LIBDIR        := $boost_libdir
Q_BOOST_PYTHON_LIB    := $boost_python_lib

Q_ENABLE_OPENGLX      := $enable_openglx
Q_ENABLE_OPENGLXM     := $enable_openglxm
Q_ENABLE_RAYTRACERX   := $enable_raytracerx

EOF
echo "done"

echo -n "Writing config.status ... "
echo $argv > config.status
echo "done"

# ---
echo ""
echo -n "Enabled support for"
if ( $enable_openglx == 1 ) then
  echo -n " openglx"
endif

if ( $enable_openglxm == 1 ) then
  echo -n " openglxm"
endif

if ( $enable_raytracerx == 1 ) then
  echo -n " raytracerx"
endif

echo "."

# ---
echo ""
echo "To build Geant4Py type:"
echo ""
echo "  make"
echo "  make install"
echo ""

exit(0)

# ======================================================================
# help message
# ======================================================================
HELP:
cat <<EOF
\`configure' configures Geant4Py to adapt to many kinds of systems.

Usage:  ./configure SYSTEM [OPTION]... [VAR=VALUE]...

SYSTEM: System type (see Supported Arhitectures)

Options:
  -h, --help                Display this help and exit

Installation directories:
  --prefix=PREFIX           Installation prefix  [./]
  --libdir=DIR              Python modules installaion dir [PREFIX/lib]
  --incdir=DIR              Header installaion dir [PREFIX/include]

Fine tuning of the library path:
  --with-g4-incdir=DIR      Geant4 header dir [\$G4INCLUDE]
  --with-g4-libdir=DIR      Geant4 library dir [\$G4LIB/\$G4SYSTEM]
  --with-g4-version=NUM     Geant4 version (700/701/710/711)
                            Not required after ver.8.0

  --with-clhep-incdir=DIR   CLHEP header dir [\$CLHEP_INCLUDE_DIR]
  --with-clhep-libdir=DIR   CLHEP library dir [\$CLHEP_LIB_DIR]
  --with-clhep-lib=LIB      library name of libCLHEP.so [CLHEP|\$CLHEP_LIB]

  --with-python-incdir=DIR  Python header dir [/usr/include/python],
                            (location of pyconfig.h)
  --with-python-libdir=DIR  Python library dir [/usr/lib]

  --with-boost-incdir=DIR   BOOST-C++ header dir [/usr/include], 
                            (location of boost/)
  --with-boost-libdir=DIR   BOOST-C++ library dir [/usr/lib]
  --with-boost-python-lib=LIB library name of libboost_python.so [boost_python]

  --with-package-dir=DIR    Geant4 Package dir

Enable/disable options: prefix with either --enable- or --disable-
  openglx      OpenGLX support    [auto]
  openglxm     OpenGLXm support   [disable, \$G4VIS_USE_OPENGLXM]
  raytracerx   RayTracerX support [disable, \$G4VIS_USE_RAYTRACERX]

Supported Architectures:
  linux           for Linux gcc 3.x and 4.x
  linuxx8664gcc   for AMD Opteron and Intel EM64T Linux gcc 3.x and 4.x
  macosx          for MacOS X with gcc (test)

EOF
exit

